/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'lars' at '13.06.14 10:39' with Gradle 1.12
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/1.12/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'ivy-publish'


group = 'com.infodesire'
version = '0.1-SNAPSHOT'
mainClassName = 'com.infodesire.tablestream.Main'


// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {

    // The production code uses the SLF4J logging API at compile time
    compile(
    
     // xstream
     'xpp3:xpp3_min:1.1.4c',
     'com.thoughtworks.xstream:xstream:1.4.3',
	 'xmlpull:xmlpull:1.1.3.1',
	 
	 // cli
	 'commons-cli:commons-cli:1.2'
     
    )

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.11'

}

eclipse {
  classpath {
    //customizing the classes output directory:
    defaultOutputDir = file('eclipse-build')
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
	publications {
		ivyJava( IvyPublication ) {
			from components.java
		}
	}
	repositories {
		ivy {
	    	url 'file://s/repository/maven'
	    	layout 'pattern' , {
	            artifact '[organisation].[module]/jars/[artifact]-[revision](.[ext])'
	        }
	    }
	}
}

// MAC OS X: do not show dock icon:
startScripts{
    doLast{
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="-Dapple.awt.UIElement=true"')    
    }
}